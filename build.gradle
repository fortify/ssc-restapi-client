plugins {
  id 'java'
  id 'idea'
  id ('org.openapi.generator') version('5.4.0') //we can't update now due to okhttp-gson-nextgen validates json attributes, but SSC sends an additional undocumented _href attribute
  id 'maven-publish'
  id 'signing'
  id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
}

repositories {
  mavenCentral()
}

group = 'com.fortify'
version = '24.2'
description 'Fortify SSC REST API client'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

ext {
    modifiedSpecDir = "$buildDir/tmp"
    originalSpecDir = "src/swagger/spec.json"
    generatedCodeDir = "$buildDir/generatedCode"
}


dependencies {

  implementation 'com.squareup.okhttp3:okhttp:4.12.0'
  implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'
  implementation 'com.google.code.gson:gson:2.10.1'
  implementation 'io.gsonfire:gson-fire:1.9.0'
  implementation 'com.google.code.findbugs:jsr305:3.0.2'
  implementation 'io.swagger:swagger-annotations:1.6.13'
  implementation 'jakarta.annotation:jakarta.annotation-api:1.3.5'
  implementation 'com.squareup.okio:okio-jvm:3.8.0'
  implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.9.23'
  implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.9.23'
  implementation 'org.jetbrains.kotlin:kotlin-stdlib-common:1.9.23'
  testImplementation 'junit:junit:4.13.2'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives(sourcesJar) {
        type 'source'
    }
    archives(javadocJar) {
        type 'javadoc'
    }
}

task fixAndCopySwaggerSpec(type: Sync) {
    description 'Read spec-rest.json and remove « and » characters and rename [Custom Tag] to [Custom Tag Audit] to avoid collision with [Custom tag] (lower case), also rename URI to URI_1 to avoid collision with java.net.URI in Resource.java. Write modified file to task temporaryDir'
    into(modifiedSpecDir)
    from(originalSpecDir) {
        filteringCharset = "UTF-8"
        filter({ String line ->
            line.replaceAll(/[\u00ab\u00bb]/, "")    // [«»]
                .replaceAll(/([\/<"])Custom Tag([>"])/, '$1Custom Tag Audit$2')    // upper case [Custom Tag]
                .replaceAll(/([\/"])URI"/, '$1URI_1"')    // avoid collision with java.net.URI
        })
    }
}

openApiGenerate {
    generatorName = "java"
    inputSpec = "$modifiedSpecDir/spec.json"
    apiPackage = "com.fortify.ssc.restclient.api"
    invokerPackage = "com.fortify.ssc.restclient"
    outputDir = generatedCodeDir
    modelPackage = 'com.fortify.ssc.restclient.model'
    generateModelDocumentation = true
    generateModelTests = false
    generateApiDocumentation = false
    generateApiTests = false
    configOptions = [
        dateLibrary: "java8",
        library: "okhttp-gson",
        serializationLibrary: "gson",
        openApiNullable: "false",
        hideGenerationTimestamp: "true",
        annotationLibrary: "none",  //this is only available in openapi generator v6.x
        useJakartaEe: "true" //this is only available in openapi generator v7.x
    ]
}


tasks.named("openApiGenerate").configure {
    dependsOn tasks.fixAndCopySwaggerSpec
}

compileJava.dependsOn tasks.openApiGenerate
sourceSets.main.java.srcDir generatedCodeDir

// codegen creates javadoc with tags which are unsupported
// in HTML 5. We disable doclint to avoid build procedure failure
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                groupId = project.group
                artifactId = project.name
                name = project.name
                version = project.version
                description = project.description
                url = 'https://github.com/fortify/ssc-restapi-client'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'fortify'
                        name = 'Fortify'
                        email = 'fortify-oss@fortify.com'
                        organization = 'Open Text Fortify'
                        organizationUrl = 'https://www.microfocus.com/en-us/cyberres/application-security'
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/fortify/${rootProject.name}.git"
                    developerConnection = "scm:git:https://github.com/fortify/${rootProject.name}.git"
                    url = "https://github.com/fortify/${rootProject.name}"
                }
            }
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

// Sign using signingKey and signingPassword properties
signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    required { gradle.taskGraph.hasTask("publishToOSSRH") }
    sign publishing.publications.mavenJava
}

// Publish using OSSRHUsername and OSSRHPassword properties
nexusPublishing {
    repositories {
        OSSRH {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

